; THIS CODE WAS COPIED ALMOST ENTIRELY FROM COBRASOV.COM.
;
; THE ORIGINAL CODE IS EXTENSIVELY DOCUMENTED HERE:
; http://cobrasov.com/CoBra%20Project/basic+nmi-ro.html
; http://cobrasov.com/CoBra%20Project/basic+nmi.html
;
; THE ROUTINE COPIES SNAPSHOT MEMORY INTO PLACE BEFORE JUMPING TO THE RESTORE
; ROUTINE IN SPECTRUM-COMPTABILITY MODE.  I HAVE MODIFIED IT TO WORK WITH
; RUNZ80 AND TO RESTORE AY-3 STATE AND BORDER COLOR.

LOADER:
	DI
	LD HL,Z80MEMEND
	LD DE,0FFFFH
	LD BC,0C000H
	LDDR
	LD BC,1B00H
	LD HL,4000H
LOADER_LOOP1:
	LD D,(HL)
	LD A,40H
	OUT (0FEH),A
	LD (HL),D
	XOR A
	OUT (0FEH),A
	INC HL
	DEC BC
	LD A,B
	OR C
	JR NZ,LOADER_LOOP1
	LD A,03H
	OUT (0E3H),A
	OUT (0EBH),A
	OUT (0F3H),A
	OUT (0FBH),A
	XOR A
	OUT (0FDH),A
	LD A,(BORDER)		; SET BORDER
	OUT (0FEH),A
LOADER_LOOP2:
	LD A,0
	IN A,(0FEH)
	AND 3FH
	CP 3FH
	JR NZ,LOADER_SKIP
	JR LOADER_LOOP2
LOADER_SKIP:
	LD HL,AY3REGS+16	; RESTORE AY3 REGISTERS
	LD A,16
LOADER_AY3LOOP:
	DEC HL
	DEC A
	LD D,(HL)
	LD BC,0FFFDH
	OUT (C),A
	LD BC,0BFFDH
	OUT (C),D
	AND A
	JR NZ,LOADER_AY3LOOP
	DEC HL
	LD A,(HL)
	LD BC,0FFFDH
	OUT (C),A
	LD BC,0
	LD SP,Z80RESTORE	; MOVING ON
	LD HL,Z80RESTORE
	EXX
	LD HL,0038H
	LD A,(BORDER)
	OR 0C0H
	OUT (0FEH),A
	LD R,A
	JP (HL)
