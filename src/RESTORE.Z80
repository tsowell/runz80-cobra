; THIS CODE WAS COPIED ALMOST ENTIRELY FROM COBRASOV.COM.
; 
; THE ORIGINAL CODE IS EXTENSIVELY DOCUMENTED HERE:
; http://cobrasov.com/CoBra%20Project/basic+nmi-ro.html
; http://cobrasov.com/CoBra%20Project/basic+nmi.html
;
; I HAVE MODIFIED THE ORIGINAL WITH PLACEHOLDER VALUES (0) SO THAT IT CAN BE
; ASSEMBLED.  THE CODE IN GENRST.Z80 MODIFIES THE MACHINE CODE AT RUNTIME TO
; FILL IN THE REGISTER VALUES FROM THE SNAPSHOT.

RESTORE:
_RST_BORDER:	LD A,0
		OUT (0FEH),A
_RST_R:		LD A,0
		LD R,A
_RST_I:		LD A,0
		LD I,A
_RST_AF_:	LD HL,0
		PUSH HL
_RST_AF:	LD HL,0
		PUSH HL
		POP AF
		EX AF,AF'
		POP AF
		EX AF,AF'
_RST_BC:	LD BC,0
_RST_DE:	LD DE,0
_RST_HL:	LD HL,0
_RST_SP:	LD SP,0
_RST_IX:	LD IX,0
_RST_IY:	LD IY,0
		EXX
_RST_BC_:	LD BC,0
_RST_DE_:	LD DE,0
_RST_HL_:	LD HL,0
		EXX
RST_EIDI:	DI
_RST_IM:	IM 0
_RST_PC:	JP 0

; ADDRESSES OF OPERANDS IN THE ASSEMBLED CODE
RST_BORDER EQU _RST_BORDER + 1
RST_R EQU _RST_R + 1
RST_I EQU _RST_I + 1
RST_F_ EQU _RST_AF_ + 1
RST_A_ EQU _RST_AF_ + 2
RST_F EQU _RST_AF + 1
RST_A EQU _RST_AF + 2
RST_BC EQU _RST_BC + 1
RST_DE EQU _RST_DE + 1
RST_HL EQU _RST_HL + 1
RST_SP EQU _RST_SP + 1
RST_IX EQU _RST_IX + 2
RST_IY EQU _RST_IY + 2
RST_BC_ EQU _RST_BC_ + 1
RST_DEÿ_ EQU _RST_DE_ + 1
RST_HL_ EQU _RST_HL_ + 1
RST_IM EQU _RST_IM + 1
RST_PC EQU _RST_PC + 1
RESTOREEND:
